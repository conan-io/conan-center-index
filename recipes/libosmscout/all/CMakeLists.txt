cmake_minimum_required(VERSION 3.10)
project(cmake_wrapper)

include(conanbuildinfo.cmake)
conan_basic_setup()

macro(find_fix_package conan_name)
  find_package(${conan_name})
  if(${conan_name}_FOUND)
    string(TOUPPER ${conan_name} upper_name)
    set(suffixes FOUND INCLUDE_DIR INCLUDE_DIRS LIBRARIES)
    foreach(s ${suffixes})
      set(${upper_name}_${s} ${${conan_name}_${s}})
    endforeach()
  endif()
endmacro(find_fix_package)

find_package(Protobuf)
if(Protobuf_FOUND)
  # https://github.com/conan-io/conan-center-index/issues/3492
  set(Protobuf_INCLUDE_DIRS ${protobuf_INCLUDE_DIRS})
  set(Protobuf_LIBRARIES ${protobuf_LIBRARIES})
endif()
find_fix_package(cairo)
find_fix_package(Freetype)
if(FREETYPE_FOUND)
  set(FREETYPE_INCLUDE_DIR_freetype2 ${FREETYPE_INCLUDE_DIR})
  set(FREETYPE_INCLUDE_DIR_ft2build ${FREETYPE_INCLUDE_DIR})
endif()
find_fix_package(glib)
find_fix_package(glm)
find_fix_package(harfbuzz)
find_fix_package(Iconv)
find_fix_package(pango)
if(PANGO_FOUND)
  if(GLIB_FOUND)
    set(PANGO_INCLUDE_DIRS ${PANGO_INCLUDE_DIRS} ${GLIB_INCLUDE_DIRS})
  endif()
  if(CAIRO_FOUND)
    set(PANGOCAIRO_FOUND 1)
  endif()
endif()

add_subdirectory(source_subfolder)
