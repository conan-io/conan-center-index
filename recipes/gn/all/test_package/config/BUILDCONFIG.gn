# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (target_os == "") {
  target_os = host_os
}
if (target_cpu == "") {
  target_cpu = host_cpu
}
if (current_cpu == "") {
  current_cpu = target_cpu
}
if (current_os == "") {
  current_os = target_os
}
is_windows = host_os == "win" && current_os == "win" && target_os == "win"
is_linux = host_os == "linux" && current_os == "linux" && target_os == "linux"
is_mac = host_os == "mac" && current_os == "mac" && target_os == "mac"
# All binary targets will get this list of configs by default.
_shared_binary_target_configs = [ "//config:compiler_defaults" ]
# Apply that default list to the binary target types.
set_defaults("executable") {
  configs = _shared_binary_target_configs
  # Executables get this additional configuration.
  configs += [ "//config:executable_ldconfig" ]
}
set_defaults("static_library") {
  configs = _shared_binary_target_configs
}
set_defaults("shared_library") {
  configs = _shared_binary_target_configs
}
set_defaults("source_set") {
  configs = _shared_binary_target_configs
}
if (is_windows) {
  set_default_toolchain("//config/toolchain:msvc")
} else {
  set_default_toolchain("//config/toolchain:gcc")
}

print("current_os:", current_os)
print("current_cpu:", current_cpu)
print("host_os:", host_os)
print("host_cpu:", host_cpu)
print("target_os:", target_os)
print("target_cpu:", target_os)
