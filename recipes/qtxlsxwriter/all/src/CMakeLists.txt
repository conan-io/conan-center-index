cmake_minimum_required(VERSION 3.1)

find_package(qt REQUIRED CONFIG)

include(extras)
include(Qt5CoreMacros)

set(QTXLSXWRITER_TARGET_NAME qtxlsxwriter)

file(GLOB QTXLSXWRITER_HEADERS LIST_DIRECTORIES=false xlsx/*.h)
file(GLOB QTXLSXWRITER_SOURCES LIST_DIRECTORIES=false xlsx/*.cpp)

# automoc cannot be used atm
# because the current qt config generated by the CMakeDeps generator
# does not provide necessary targets (ex. Qt5::moc)
qt5_wrap_cpp(QTXLSXWRITER_SOURCES xlsx/xlsxdocument.h)

add_library(${QTXLSXWRITER_TARGET_NAME}
	${QTXLSXWRITER_HEADERS}
	${QTXLSXWRITER_SOURCES}
)

target_include_directories(${QTXLSXWRITER_TARGET_NAME}
	PRIVATE
		$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/xlsx>
		# qtxlsxwriter uses qt private headers
		$<BUILD_INTERFACE:${QT_ROOT}/include/QtCore/5.15.2>
		$<BUILD_INTERFACE:${QT_ROOT}/include/QtGui/5.15.2>
		$<BUILD_INTERFACE:${QT_ROOT}/include/QtGui/5.15.2/QtGui>
	INTERFACE
		$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/xlsx>
		$<INSTALL_INTERFACE:include>
)

target_compile_features(${QTXLSXWRITER_TARGET_NAME}
	PRIVATE
		cxx_range_for
)

# place qt::Gui before qt::Core (important for some toolchains)
# since the current qt recipe provides an incorrect order of libraries
target_link_libraries(${QTXLSXWRITER_TARGET_NAME}
	$<$<TARGET_EXISTS:CONAN_LIB::qt_Qt5Gui_RELEASE>:CONAN_LIB::qt_Qt5Gui_RELEASE>
	$<$<TARGET_EXISTS:CONAN_LIB::qt_Qt5Gui_DEBUG>:CONAN_LIB::qt_Qt5Gui_DEBUG>
	$<$<TARGET_EXISTS:CONAN_LIB::qt_Qt5Gui_RELWITHDEBINFO>:CONAN_LIB::qt_Qt5Gui_RELWITHDEBINFO>
	$<$<TARGET_EXISTS:CONAN_LIB::qt_Qt5Gui_MINSIZEREL>:CONAN_LIB::qt_Qt5Gui_MINSIZEREL>
)

target_link_libraries(${QTXLSXWRITER_TARGET_NAME}
	qt::qt
)

get_target_property(target_type ${QTXLSXWRITER_TARGET_NAME} TYPE)
if (target_type STREQUAL "STATIC_LIBRARY")
	target_compile_definitions(${QTXLSXWRITER_TARGET_NAME}
		PUBLIC
			QTXLSX_STATIC
	)
endif()

set_target_properties(${QTXLSXWRITER_TARGET_NAME} PROPERTIES
	DEFINE_SYMBOL QT_BUILD_XLSX_LIB
)

set_property(TARGET ${QTXLSXWRITER_TARGET_NAME} PROPERTY PUBLIC_HEADER
	xlsx/xlsxabstractooxmlfile.h
	xlsx/xlsxabstractsheet.h
	xlsx/xlsxcell.h
	xlsx/xlsxcellformula.h
	xlsx/xlsxcellrange.h
	xlsx/xlsxcellreference.h
	xlsx/xlsxchart.h
	xlsx/xlsxchartsheet.h
	xlsx/xlsxconditionalformatting.h
	xlsx/xlsxdatavalidation.h
	xlsx/xlsxdocument.h
	xlsx/xlsxformat.h
	xlsx/xlsxglobal.h
	xlsx/xlsxrichstring.h
	xlsx/xlsxworkbook.h
	xlsx/xlsxworksheet.h
)

# define installing rules for target files
install(TARGETS ${QTXLSXWRITER_TARGET_NAME}
	PUBLIC_HEADER DESTINATION include
)
