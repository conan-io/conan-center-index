# This script should only run when included in a FindXXX.cmake module
if(NOT OpenSSL_LIB_DIRS)
    return()
endif()

# library names of crypto and ssl libraries
set(CONAN_CRYPTO_LIBRARY "@CONAN_CRYPTO_LIBRARY@")
set(CONAN_SSL_LIBRARY "@CONAN_SSL_LIBRARY@")

# absolute paths of crypto and ssl libraries
find_library(CONAN_CRYPTO_ABSOLUTE_LIBRARY NAME ${CONAN_CRYPTO_LIBRARY} PATHS ${OpenSSL_LIB_DIRS} NO_DEFAULT_PATH NO_CMAKE_FIND_ROOT_PATH)
find_library(CONAN_SSL_ABSOLUTE_LIBRARY NAME ${CONAN_SSL_LIBRARY} PATHS ${OpenSSL_LIB_DIRS} NO_DEFAULT_PATH NO_CMAKE_FIND_ROOT_PATH)

# https://cmake.org/cmake/help/latest/module/FindOpenSSL.html

set(OPENSSL_FOUND ON)
set(OPENSSL_INCLUDE_DIR ${OpenSSL_INCLUDE_DIRS})
set(OPENSSL_LIBRARIES ${OpenSSL_LIBRARIES} ${OpenSSL_SYSTEM_LIBS})
set(OPENSSL_VERSION "${OpenSSL_VERSION}")

set(OPENSSL_CRYPTO_LIBRARY ${CONAN_CRYPTO_ABSOLUTE_LIBRARY})
set(OPENSSL_CRYPTO_LIBRARIES ${CONAN_CRYPTO_ABSOLUTE_LIBRARY} ${OpenSSL_SYSTEM_LIBS})
set(OPENSSL_SSL_LIBRARY ${CONAN_CRYPTO_ABSOLUTE_LIBRARY})
set(OPENSSL_SSL_LIBRARIES ${CONAN_CRYPTO_ABSOLUTE_LIBRARY} ${OpenSSL_SYSTEM_LIBS})

if(NOT CMAKE_VERSION VERSION_LESS 3.0)
    if(NOT TARGET OpenSSL::Crypto)
        add_library(OpenSSL::Crypto INTERFACE IMPORTED)
        if(OpenSSL_INCLUDE_DIRS)
            set_target_properties(OpenSSL::Crypto PROPERTIES INTERFACE_INCLUDE_DIRECTORIES "${OpenSSL_INCLUDE_DIRS}")
        endif()
        set_property(TARGET OpenSSL::Crypto PROPERTY INTERFACE_LINK_LIBRARIES ${CONAN_CRYPTO_ABSOLUTE_LIBRARY} ${OpenSSL_SYSTEM_LIBS} "${OpenSSL_LINKER_FLAGS_LIST}")
        set_property(TARGET OpenSSL::Crypto PROPERTY INTERFACE_COMPILE_DEFINITIONS ${OpenSSL_COMPILE_DEFINITIONS})
        set_property(TARGET OpenSSL::Crypto PROPERTY INTERFACE_COMPILE_OPTIONS "${OpenSSL_COMPILE_OPTIONS_LIST}")
    endif()
    if(NOT TARGET OpenSSL::SSL)
        add_library(OpenSSL::SSL INTERFACE IMPORTED)
        if(OpenSSL_INCLUDE_DIRS)
            set_target_properties(OpenSSL::SSL PROPERTIES INTERFACE_INCLUDE_DIRECTORIES "${OpenSSL_INCLUDE_DIRS}")
        endif()
        set_property(TARGET OpenSSL::SSL PROPERTY INTERFACE_LINK_LIBRARIES ${CONAN_SSL_ABSOLUTE_LIBRARY} ${OpenSSL_SYSTEM_LIBS} "${OpenSSL_LINKER_FLAGS_LIST}")
        set_property(TARGET OpenSSL::SSL PROPERTY INTERFACE_COMPILE_DEFINITIONS ${OpenSSL_COMPILE_DEFINITIONS})
        set_property(TARGET OpenSSL::SSL PROPERTY INTERFACE_COMPILE_OPTIONS "${OpenSSL_COMPILE_OPTIONS_LIST}")
    endif()
endif()
